# Zsh Plugin Standard

## 1. Standarized $0 handling

Plugins should do:

```zsh
0="${${ZERO:-${0:#$ZSH_ARGZERO}}:-${(%):-%N}}"
# or
ZERO="${${ZERO:-${0:#$ZSH_ARGZERO}}:-${(%):-%N}}"

# Then ${0:h} or ${ZERO:h} to get plugin's directory
```

to get plugin path. The one-line code above will:

1. Use `ZERO` if it's not empty
  * plugin manager will be easily able to alter effective `$0` before loading a plugin
  * this will allow to e.g. do `eval "$(<plugin)"`, which can be faster than `source`
    (link:http://www.zsh.org/mla/workers/2017/msg01827.html[comparison], note it's not for a compiled script)
2. Use `$0` if it doesn't contain path to Zsh binary
  * plugin manager will still be able to set `$0`, although more difficultly
  * `setopt no_function_argzero` will be detected (it causes `$0` not to contain plugin-script path, but path
    to Zsh binary)
  * `setopt posix_argzero` will be detected (as above)
3. Use `%N` prompt expansion flag, which always gives absolute path to script
  * plugin manager cannot alter this, but simple source will be saved from breaking by the mentioned
    `*_argzero` options

The goal is flexibility, with essential motivation to support `eval "$(<plugin)"` and definitely
solve `setopt no_function_argzero` and `setopt posix_argzero` cases.

A plugin manager will be even able to convert plugin to function, but performance differences of this are
unclear. It might however provide an use case.

## 2. Unload function

If plugin is named e.g. `kalc`, then it can provide function `kalc_unload_plugin`,
which can be called by a plugin manager to withdraw effects of loading this
plugin.

Plugin manager can implement its own tracking of changes made by a plugin, so this
is in general optional, however to properly unload e.g. a prompt, detailed tracking
(easy to do by the plugin creator) can provide better, predictable results.

## 3. Plugin manager activity indicator

Plugin manager should set `$LOADED_PLUGINS` array containing all previously loaded
plugins and currently loaded plugin (on last index). This will allow plugin to:

 1. Check which plugins are already loaded.
 2. Check if it is loaded by a plugin manager.

First item will allow the plugin to e.g. issue a notice about missing dependencies.
Instead of issuing a notice, it will be able to satisfy the dependencies from resources
it provides. For example, `pure` prompt provides `zsh-async` dependency library, which
is a separate project. In result, the prompt can source this plugin, having `$ZERO`, etc.
(it doesn't normally source the dependency).

Second item will allow plugin to e.g. set up `$fpath`, knowing that plugin manager will
not handle this:

```zsh
if [[ ( ${+LOADED_PLUGINS} = 0 || ${LOADED_PLUGINS[-1]} != */kalc ) && -z "${fpath[(r)$ZERO]}" ]]; then
    fpath+=( "$ZERO" )
fi
```

This will allow user to source the plugin without using a plugin manager.

## 4. Global parameter with PREFIX for make, configure, etc.

Plugin manager should export parameter `$ZPFX` which should contain path to directory dedicated
for user-land software, i.e. for directories `$ZPFX/bin`, `$ZPFX/lib`, `$ZPFX/share`, etc.
Suggested name of the directory is `polaris`, Zplugin uses this name and places this directory
at `~/.zplugin/polaris` by default.

User can then configure hooks (feature of e.g. zplug and Zplugin) to invoke e.g. `make PREFIX=$ZPFX install`
to install software like e.g. link:https://github.com/tj/git-extras[tj/git-extras]. This is
a developing role of Zsh plugin managers as package managers, where `.zshrc` has similar role to
Chef or Puppet configuration and allows to declare system state, and have the same state on
different accounts.

No-narration facts-list:

 1. `export ZPFX="$HOME/polaris"` (or e.g. `$HOME/.zplugin/polaris`)
 2. `make PREFIX=$ZPFX install`
 3. `configure --prefix=$ZPFX`
 4. `cmake -DCMAKE_INSTALL_PREFIX=$ZPFX .`
 5. `zplugin ice make"PREFIX=$ZPFX install"`
 6. `zplug ... hook-build:"make PREFIX=$PFX install"`
