# Zsh Plugin Standard

## What is a Zsh plugin?

Zsh plugins were first defined by Oh-My-Zsh. They are dramatically simple yet powerful.
From analytical perspective, a plugin:

1. Has its directory added to `$fpath`.
2. It has any first `\*.plugin.zsh` file sourced (or `*.zsh`, `init.zsh`, `*.sh`, these are non-standard).

First point allows plugins to provide completions and use `autoload` functions (a single function per file
solution, Zsh feature). Second point isn't so explicitly useful, but having whole Zsh community agreed on
single extension standard is something worth noticing.

From a more broad perspective, a plugin is:

1. A directory with various files (main script, autoload functions, completions, Makefiles, backend
   programs, documentation).
2. A script that obtains path to its directory via `$0` (see link:#zero-handling[next section] for
   enhancement proposal).
3. A Github (or other site) repository identified by two components **username**/**pluginname**.
4. A software package with any type of command line artifact – when used with advanced plugin
   managers that have hooks, can run Makefiles, add directories to `$PATH`.

Below follow proposed enhancements and codifications of "Zsh plugin" meaning and plugin managers'
functions – the proposed standardization. +
 +

'''

[#zero-handling]
## 1. Standardized $0 handling

Plugins should do:

```zsh
0="${${ZERO:-${0:#$ZSH_ARGZERO}}:-${(%):-%N}}"
# or
ZERO="${${ZERO:-${0:#$ZSH_ARGZERO}}:-${(%):-%N}}"

# Then ${0:h} or ${ZERO:h} to get plugin's directory
```

to get plugin-file path. The one-line code above will:

1. Be backwards-compatible with normal `$0` setting and usage.
2. Use `ZERO` if it's not empty,
  * plugin manager will be easily able to alter effective `$0` before loading a plugin,
  * this will allow to e.g. do `eval "$(<plugin)"`, which can be faster than `source`
    (link:http://www.zsh.org/mla/workers/2017/msg01827.html[comparison], note it's not for a compiled script).
3. Use `$0` if it doesn't contain path to Zsh binary,
  * plugin manager will still be able to set `$0`, although more difficultly (requires `unsetopt function_argzero`
    before sourcing plugin script, and `0=...` assignment),
  * `unsetopt function_argzero` will be detected (it causes `$0` not to contain plugin-script path, but path
    to Zsh binary, if not overwritten by `0=...` assignment),
  * `setopt posix_argzero` will be detected (as above).
4. Use `%N` prompt expansion flag, which always gives absolute path to script,
  * plugin manager cannot alter this (no advanced loading of plugin is possible), but simple plugin-file
    sourcing (without a plugin manager) will be saved from breaking caused by the mentioned `*_argzero`
    options, so this is a very good last-resort fallback.

The goal is flexibility, with essential motivation to support `eval "$(<plugin)"` and definitely
solve `setopt no_function_argzero` and `setopt posix_argzero` cases.

A plugin manager will be even able to convert plugin to function (author implemented such proof of concept
functionality, it's possible), but performance differences of this are unclear. It might however provide an
use case.

[#unload-fun]
## 2. Unload function

If plugin is named e.g. `kalc`, then it can provide function `kalc_unload_plugin`,
which can be called by a plugin manager to withdraw effects of loading this
plugin.

Plugin manager can implement its own tracking of changes made by a plugin, so this
is in general optional, however to properly unload e.g. a prompt, detailed tracking
(easy to do by the plugin creator) can provide better, predictable results. Any
special, uncommon effects of loading a plugin are possible to withdraw only by a
dedicated function.

[#indicator]
## 3. Plugin manager activity indicator

Plugin manager should set `$LOADED_PLUGINS` array containing all previously loaded
plugins and plugin being currently loaded (on last index). This will allow plugin to:

 1. Check which plugins are already loaded.
 2. Check if it is being loaded by a plugin manager (i.e. if not just sourced).

First item will allow the plugin to e.g. issue a notice about missing dependencies.
Instead of issuing a notice, it will be able to satisfy the dependencies from resources
it provides. For example, `pure` prompt provides `zsh-async` dependency library, which
is a separate project and can be loaded by user on his own behalf. In result, the prompt
can decide to source its private copy of `zsh-async`, having also reliable `$ZERO` defined
by previous section (note: `pure` doesn't normally do this).

Second item will allow plugin to e.g. set up `$fpath`, knowing that plugin manager will
not handle this:

```zsh
if [[ ( ${+LOADED_PLUGINS} = 0 || ${LOADED_PLUGINS[-1]} != */kalc ) && -z "${fpath[(r)${ZERO:h}]}" ]]; then
    fpath+=( "${ZERO:h}" )
fi
```

This will allow user to reliably source the plugin without using a plugin manager.

[#zpfx]
## 4. Global parameter with PREFIX for make, configure, etc.

Plugin manager should export parameter `$ZPFX` which should contain path to directory dedicated
for user-land software, i.e. for directories `$ZPFX/bin`, `$ZPFX/lib`, `$ZPFX/share`, etc.
Suggested name of the directory is `polaris`, Zplugin uses this name and places this directory
at `~/.zplugin/polaris` by default.

User can then configure hooks (feature of e.g. zplug and Zplugin) to invoke e.g. `make PREFIX=$ZPFX install`
to install software like e.g. link:https://github.com/tj/git-extras[tj/git-extras]. This is a
developing role of Zsh plugin managers as package managers, where `.zshrc` has a similar role
to Chef or Puppet configuration and allows to **declare** system state, and have the same state
on different accounts / machines.

No-narration facts-list related to `$ZPFX`:

 1. `export ZPFX="$HOME/polaris"` (or e.g. `$HOME/.zplugin/polaris`)
 2. `make PREFIX=$ZPFX install`
 3. `./configure --prefix=$ZPFX`
 4. `cmake -DCMAKE_INSTALL_PREFIX=$ZPFX .`
 5. `zplugin ice make"PREFIX=$ZPFX install"`
 6. `zplug ... hook-build:"make PREFIX=$PFX install"`
